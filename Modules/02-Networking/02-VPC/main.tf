resource "aws_vpc" "this" {
  cidr_block           = var.vpc_cidr
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name        = var.vpc_name
    Environment = var.environment
    Projectname = var.projectname
  }
}

resource "aws_internet_gateway" "this" {
  vpc_id = aws_vpc.this.id

  tags = {
    Name        = var.igw_name
    Environment = var.environment
    Projectname = var.projectname
  }
}

# Public subnets — for_each over map: key = logical name, value = { cidr, az }
resource "aws_subnet" "public" {
  for_each = var.public_subnets

  vpc_id                                      = aws_vpc.this.id
  cidr_block                                  = each.value.cidr
  availability_zone                           = each.value.az
  map_public_ip_on_launch                     = true
  enable_resource_name_dns_a_record_on_launch = true

  tags = {
    Name        = "${var.vpc_name}-public-${each.key}"
    Environment = var.environment
    Projectname = var.projectname
  }
}

# Private subnets — for_each over map: key = logical name, value = { cidr, az }
resource "aws_subnet" "private" {
  for_each = var.private_subnets

  vpc_id     = aws_vpc.this.id
  cidr_block = each.value.cidr
  availability_zone = each.value.az

  tags = {
    Name        = "${var.vpc_name}-private-${each.key}"
    Environment = var.environment
    Projectname = var.projectname
  }
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.this.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.this.id
  }

  tags = {
    Name        = "${var.vpc_name}-public-rt"
    Environment = var.environment
    Projectname = var.projectname
  }
}

resource "aws_route_table_association" "public" {
  for_each       = aws_subnet.public
  subnet_id      = each.value.id
  route_table_id = aws_route_table.public.id
}

resource "aws_route_table" "private" {
  vpc_id = aws_vpc.this.id

  tags = {
    Name        = "${var.vpc_name}-private-rt"
    Environment = var.environment
    Projectname = var.projectname
  }
}

resource "aws_route_table_association" "private" {
  for_each       = aws_subnet.private
  subnet_id      = each.value.id
  route_table_id = aws_route_table.private.id
}
